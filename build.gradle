ext {
    preDexLibs = !project.hasProperty('disablePreDex')

    version = '0.9.0'
    versionCode = 30
    minSdkVersion = 16
    targetSdkVersion = 25
    compileSdkVersion = 25
    buildToolsVersion = '25.0.3'

    // Frameworks and code only libraries
    kotlinVersion = '1.1.2-3'
    ankoVersion = '0.10.0-beta-2'
    ankoSqliteVersion = '0.9.1a'
    supportLibraryVersion = '25.3.1'
    supportPreferenceVersion = '1.2.7'
    playServicesVersion = '10.2.4'
    eventBusVersion = '3.0.0'
    eventBusAnnotationVersion = '3.0.1'
    androidJobVersion = '1.1.11-SNAPSHOT'
    proxerLibVersion = 'new-architecture-SNAPSHOT'
    ktaskVersion = '0.3.4'

    // UI components
    aboutLibrariesVersion = '5.9.6'
    materialDrawerVersion = '5.9.1'
    iconicsVersion = '2.8.4'
    communityMaterialTypefaceVersion = '1.9.32.1'
    crossfaderVersion = '1.5.0'
    materialAboutVersion = '1.9.0'
    subsamplingScaleImageViewVersion = '3.6.0'
    circleImageViewVersion = '2.1.0'
    tabLayoutHelperVersion = '0.8.0'
    materialDialogsVersion = '0.9.4.4'
    flexboxVersion = '0.2.6'
    chipCloudVersion = '3.0.3'
    emojiIosVersion = '0.5.0-SNAPSHOT'
    exomediaVersion = '4.0.1'
    exoplayerVersion = 'r2.4.0'
    materialProgressBarVersion = '1.4.1'
    materialRatingBarVersion = '1.0.2'
    customtabsHelperVersion = '1.0.3'
    introductionVersion = '1.4.0'

    // Utils
    threetenVersion = '1.0.5'
    kotterknifeVersion = '0.1.0-SNAPSHOT'
    hawkVersion = '2.0.1'
    glideVersion = '3.8.0'
    glideIntegrationVersion = '1.5.0'
    okHttpVersion = '3.7.0'
    okioVersion = '1.13.0'
    linkBuilderVersion = '1.5.2'
    easyHeaderFooterAdapterVersion = '1.0.6'

    // Debugging
    strictModeCompatVersion = "25.0.0"
    leakCanaryVersion = '1.5.1'

    deps = [
            kotlinStdlib                 : "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
            anko                         : "org.jetbrains.anko:anko-common:$ankoVersion",
            ankoSqlite                   : "org.jetbrains.anko:anko-sqlite:$ankoSqliteVersion",
            supportAppCompat             : "com.android.support:appcompat-v7:$supportLibraryVersion",
            supportV13                   : "com.android.support:support-v13:$supportLibraryVersion",
            supportRecyclerView          : "com.android.support:recyclerview-v7:$supportLibraryVersion",
            supportCardView              : "com.android.support:cardview-v7:$supportLibraryVersion",
            supportDesign                : "com.android.support:design:$supportLibraryVersion",
            supportAnimatedVectorDrawable: "com.android.support:animated-vector-drawable:$supportLibraryVersion",
            supportCustomtabs            : "com.android.support:customtabs:$supportLibraryVersion",
            supportPreference            : "com.android.support:preference-v7:$supportLibraryVersion",
            supportPreferenceFix         : "net.xpece.android:support-preference:$supportPreferenceVersion",
            supportPreferenceFixSpinner  : "net.xpece.android:support-spinner:$supportPreferenceVersion",
            playServicesGcm              : "com.google.android.gms:play-services-gcm:$playServicesVersion",
            eventBus                     : "org.greenrobot:eventbus:$eventBusVersion",
            eventBusAnnotation           : "org.greenrobot:eventbus-annotation-processor:$eventBusAnnotationVersion",
            androidJob                   : "com.evernote:android-job:$androidJobVersion",
            proxerLib                    : "com.github.proxer:ProxerLibAndroid:$proxerLibVersion",
            ktaskAndroid                 : "com.github.rubengees.ktask:ktask-android:$ktaskVersion",

            aboutLibraries               : "com.mikepenz:aboutlibraries:$aboutLibrariesVersion@aar",
            materialDrawer               : "com.mikepenz:materialdrawer:$materialDrawerVersion@aar",
            iconics                      : "com.mikepenz:iconics-core:$iconicsVersion@aar",
            communityMaterialTypeface    : "com.mikepenz:community-material-typeface:$communityMaterialTypefaceVersion@aar",
            crossfader                   : "com.mikepenz:crossfader:$crossfaderVersion@aar",
            materialAbout                : "com.github.daniel-stoneuk:material-about-library:$materialAboutVersion",
            subsamplingScaleImageView    : "com.davemorrissey.labs:subsampling-scale-image-view:$subsamplingScaleImageViewVersion",
            circleImageView              : "de.hdodenhof:circleimageview:$circleImageViewVersion",
            tabLayoutHelper              : "com.h6ah4i.android.tablayouthelper:tablayouthelper:$tabLayoutHelperVersion",
            materialDialogs              : "com.afollestad.material-dialogs:core:$materialDialogsVersion",
            flexbox                      : "com.google.android:flexbox:$flexboxVersion",
            chipCloud                    : "com.github.fiskurgit:ChipCloud:$chipCloudVersion",
            emojiIos                     : "com.vanniktech:emoji-ios:$emojiIosVersion",
            exomedia                     : "com.devbrackets.android:exomedia:$exomediaVersion",
            exoplayerIntegration         : "com.google.android.exoplayer:extension-okhttp:$exoplayerVersion",
            materialProgressBar          : "me.zhanghai.android.materialprogressbar:library:$materialProgressBarVersion",
            materialRatingBar            : "me.zhanghai.android.materialratingbar:library:$materialRatingBarVersion",
            customtabsHelper             : "me.zhanghai.android.customtabshelper:library:$customtabsHelperVersion",
            introduction                 : "com.github.rubengees:introduction:$introductionVersion",

            threeten                     : "com.jakewharton.threetenabp:threetenabp:$threetenVersion",
            hawk                         : "com.orhanobut:hawk:$hawkVersion",
            glide                        : "com.github.bumptech.glide:glide:$glideVersion",
            glideIntegration             : "com.github.bumptech.glide:okhttp3-integration:$glideIntegrationVersion@aar",
            okHttp                       : "com.squareup.okhttp3:okhttp:$okHttpVersion",
            okio                         : "com.squareup.okio:okio:$okioVersion",
            linkBuilder                  : "com.klinkerapps:link_builder:$linkBuilderVersion",
            easyHeaderFooterAdapter      : "com.github.rubengees:EasyHeaderFooterAdapter:$easyHeaderFooterAdapterVersion",

            strictModeCompat             : "com.kirich1409:strict-mode-compat:$strictModeCompatVersion",
            leakCanary                   : "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion",
            leakCanaryNoOp               : "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    ]
}

buildscript {
    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap-1.1" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.1.2-3"
        classpath 'com.android.tools.build:gradle:2.4.0-alpha7'
    }
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap-1.1" }
    }
}

subprojects {
    def appPlugin = "com.android.build.gradle.AppPlugin"
    def libraryPlugin = "com.android.build.gradle.LibraryPlugin"

    project.plugins.whenPluginAdded { plugin ->
        if (appPlugin == plugin.class.name || libraryPlugin == plugin.class.name) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}
